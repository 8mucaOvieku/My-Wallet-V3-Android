apply from: 'gradle/githooks.gradle'
apply from: 'scripts/tests/module_tests.gradle'

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = Versions.kotlin
        compose_version = '1.0.4'
    }
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
        maven { url 'https://cache-redirector.jetbrains.com/intellij-dependencies' }
    }
    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath Libraries.androidGradlePlugin
        classpath Libraries.googleServicesPlugin
        classpath Libraries.kotlinGradlePlugin
        classpath Libraries.kotlinSerializationPlugin
        classpath Libraries.navigationControllerSafeArgsPlugin
        classpath Libraries.firebaseCrashlyticsPlugin
        classpath Libraries.sqlDelightPlugin
        classpath Libraries.embraceIOSwazzler
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://zendesk.jfrog.io/zendesk/repo' }
        maven { url "https://cdn.veriff.me/android/" } // veriff
    }
    configurations.all {
        resolutionStrategy.force Libraries.findbugs
        resolutionStrategy.force Libraries.jacksonCore
        resolutionStrategy.force Libraries.kotlinReflect
    }
}

subprojects {
    // Increase the error output of the compiler, to prevent a flood of databinder errors
    // masking any dagger - and, indeed, other - annotation processor failures.
    afterEvaluate {
        if (project.plugins.hasPlugin("kotlin-kapt")) {
            kapt {
                javacOptions {
                    option("-Xmaxerrs", 500)
                }
            }
        }
    }
}